cmake_minimum_required(VERSION 3.10)

# Set C++ standard
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# Find Bison and Flex packages
find_package(BISON REQUIRED)
find_package(FLEX REQUIRED)

find_package(LLVM REQUIRED CONFIG)
llvm_map_components_to_libnames(llvm_libs core support native OrcJIT)

separate_arguments(LLVM_DEFINITIONS_LIST NATIVE_COMMAND ${LLVM_DEFINITIONS})

# Bison configuration: Generate parser.cpp and parser.hpp from parser.y
# BISON_TARGET(Name InputFile OutputFile)
BISON_TARGET(PolangBison parser.y ${CMAKE_CURRENT_BINARY_DIR}/parser.cpp)

# Flex configuration: Generate lexer.cpp from lexer.l
# FLEX_TARGET(Name InputFile OutputFile)
FLEX_TARGET(PolangFlex lexer.l ${CMAKE_CURRENT_BINARY_DIR}/lexer.cpp)

# Define dependency: Flex requires the header file generated by Bison
ADD_FLEX_BISON_DEPENDENCY(PolangFlex PolangBison)

add_library(PolangParser STATIC
    ${BISON_PolangBison_OUTPUTS}
    ${FLEX_PolangFlex_OUTPUTS}
)

target_include_directories(PolangParser PUBLIC
    ${CMAKE_CURRENT_BINARY_DIR}
    ${CMAKE_CURRENT_SOURCE_DIR}
    ${LLVM_INCLUDE_DIRS}
)

target_compile_definitions(PolangParser PUBLIC
    ${LLVM_DEFINITIONS_LIST}
)

target_link_directories(PolangParser PUBLIC
    ${LLVM_LIBRARY_DIRS}
)

target_link_libraries(PolangParser PUBLIC
    ${llvm_libs}
)

target_compile_options(PolangParser PRIVATE -Wno-error)

# Create the executable
add_executable(PolangRepl
    main.cpp
    codegen.cpp
)

target_link_libraries(PolangRepl PRIVATE
    PolangParser
)
